apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 2
        versionName "0.5.7"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {

        debug {
            debuggable true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.annotation:annotation:1.0.0-beta01"
    implementation 'com.google.android.material:material:1.0.0-beta01'

    // Kotlin support
    implementation "androidx.core:core-ktx:1.0.1"
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.3.31'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.31"

}

group = "net.akaish.ikey.hkb"
archivesBaseName = "ikey-hkb"
version = "0.5.7"

task deleteJar(type: Delete) {
    delete 'libs/jars/'+archivesBaseName+'.'+version+'.jar'
}

task createJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('libs/jars/')
    include('classes.jar')
    rename('classes.jar', archivesBaseName+'.'+version+'.jar')
}

createJar.dependsOn(deleteJar, build)

// Signing
apply plugin: 'signing'
signing {
    sign configurations.archives
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}
//Creating sources with comments
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
artifacts {
    archives javadocJar
    archives androidSourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name "iKeyHexKB"
                packaging 'jar'
                description 'iKeyHexKB - soft hexademical keyboard for Android'
                artifactId "ikey-hkb"
                url 'https://github.com/akaish/iKeyHexKB'
                scm {
                    connection 'scm:git:git://github.com/akaish/iKeyHexKB.git'
                    developerConnection 'scm:git:ssh://github.com/akaish/iKeyHexKB.git'
                    url 'https://github.com/akaish/iKeyHexKB/tree/master'
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'akaish'
                        name 'Denis Bogomolov'
                        email 'akaish@mail.ru'
                    }
                    developer {
                        id 'ikey-ru'
                        name 'Pavel'
                        email 'info@ikey.ru'
                    }
                }
            }
        }
    }
}